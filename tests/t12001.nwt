# test file read with using block

# need to define these in an include file

byte_stream {
	source:string,
	handle:int,
	teardown := (this:byte_stream?) -> error_list? {
		match (this)
			value {
				return close(value.handle)
			}
			| nil {
				return nil
			}
	}
}

byte_stream_setup_result {
	setup_value:byte_stream
	| setup_errors:error_list
}

open_byte_stream := (path:string, mode:stream_mode) -> byte_stream_setup_result {
	mode = mode with { binary = true } # make sure we're in binary mode
	open_result := open(path, mode)
	match (open_result)
		data {
			return @byte_stream with {
				source = path,
				handle = data
			}
		}
		| errors {
			return errors
		}
}

byte_iter {
	data:byte_result,
	next:() -> byte_iter?
}

byte_list {
	data:byte,
	next:byte_list?
}

read := (stream:byte_stream) -> byte_iter? {
	f := () -> byte_iter? {
		result := get(stream.handle)
		match(result)
			data {
				return @byte_iter with {
					data = data,
					next = f
				}
			} | errors {
				return @byte_iter with {
					data = errors
				}
			} | eof {
				return nil
			}
	}

	return f()
}

write := (stream:byte_stream, value:byte) -> error_list? {
	return put(stream.handle, value)
}

file_name := "../tests/files/abc.inout"

data := @byte_list with { data = 0x63 }
data = data with { data = 0x62, next = data }
data = data with { data = 0x61, next = data }

write_result := using open_byte_stream(file_name, @stream_mode with { write = true, binary = true, trunc = true }) as stream -> error_list? {
	for datum in data {
		r := write(stream, datum)
		match (r)
			value {
				return value # we have an error
			} | nil {}
	}

	return nil
}

read_result := using open_byte_stream(file_name, @stream_mode with { read = true, binary = true }) as stream -> error_list? {
	for value in read(stream) {
		match(value)
			data {
				print(data)
			} | errors {
				return errors
			}
	}

	return nil
}
